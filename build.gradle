plugins {
  id("java")
  id("maven-publish")

  alias(libraries.plugins.champeau.jmh)
}

group = "me.kvdpxne"
version = "0.1.0"

def targetJavaVersion = 8
java {
  def javaVersion = JavaVersion.toVersion(targetJavaVersion)

  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion

  if (JavaVersion.current() < javaVersion) {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
  }
}

dependencies {
  jmh(libraries.bundles.jmh)
  testImplementation(libraries.jupiter)
}

tasks.withType(JavaCompile).configureEach {
  if (10 <= targetJavaVersion || JavaVersion.current().isJava10Compatible()) {
    options.release.set(targetJavaVersion)
  }
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = "me.kvdpxne"
      artifactId = "forty2"
      version = "0.2.0"

      from components.java
    }
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}